## 1.2.1 Linear Recursion and Iteration

We begin by considering the factorial function, defined by

`n! = n * (n-1) * (n-2) *** 3 * 2 * 2`

There are many ways to compute factorials. One way is to make use of the observation that `n!` is equal to `n` times `(n-1)!` for any positiveinteger `n`:

`n! = n * [(n-1) * (n-2) ... * 3 * 2 * 1] = n * (n-1)!`

Thus, we can compute `n!` by computing `(n-1)!` and multiplying the result by `n`. If we add the stipulation of that `1!` is egual to `1`, this observation translates directly into a procedure:

```Lisp
(define (factorial n)
 (if (= n 1)
    1
 (* n (factorial (- n 1)))))
 ```

 ```Lisp
(factorial 7) ------------------------------------------
(* 7 (factorial 6))                                    |
(* 7 (* 6 (factorial 5)))                              |
(* 7 (* 6 (* 5 (factorial 4))))                        |
(* 7 (* 6 (* 5 (* 4 (factorial 3)))))                  |
(* 7 (* 6 (* 5 (* 4 (* 3 (factorial 2))))))            |
(* 7 (* 6 (* 5 (* 4 (* 3 (* 2 (factorial 1)))))))      |
(* 7 (* 6 (* 5 (* 4 (* 3 (* 2 1))))))                  |
(* 7 (* 6 (* 5 (* 4 (* 3 2)))))                        |
(* 7 (* 6 (* 5 (* 4 6))))                              |
(* 7 (* 6 (* 5 24)))                                   |
(* 7 (* 6 120))                                        |
(* 7 720)                                              |
5040    <-----------------------------------------------            
 ```
**Figure 1.3:** A linear recursion process for computing 7!

 We can use the subsitution model of  [Section 1.1.5](1.1.5_the_substitution_model_for_proceducre_application.md) to watch this procedure in action computing `6!`, as shown in Figure 1.3.

 Now let's take a different perspecitve on comuting factorials. We could describe a rule for computing `n!` by specifying that we first multiply 1 by 2, than multiply the result by 3, than by 4, ans so on untill we reach `n`. More formally, we maintain a running product, together with a counter that count from 1 up to `n`. We can describe the computation by saying that the counter and the product simultaneously change from one step to the next according to the rule

 ```
 product <= counter * product
 counter <= counter + 1
 ```

 and stipulating than `n!` is the value of the product when the counter exceeds `n`.

 Once again, we can recast our description as a procdure for computing factorials: *(29)*

 > *(29)* In a real program we would probably use the block structure introduced in the last section to hide the definition of `fact-iter`:

 ```Lisp
 (define (factorial n)
    (define (iter product counter)
        (if (> counter n)
            product
            (iter (* counter product)
            (+ counter 1))))
 (iter 1 1))
 ```
 > We avoided doing this here so as to minimize the number of thing to think about a once.

 ```Lisp
(factorial 7) -----------
(fact-iter 1 1 7)       |
(fact-iter 1 2 7)       |
(fact-iter 2 3 7)       |
(fact-iter 6 4 7)       |
(fact-iter 24 5 7)      |
(fact-iter 120 6 7)     |
(fact-iter 720 7 7)     |
(fact-iter 5040 8 7)    |    
5040 <------------------|
 ```

 **Figrue 1.4:** A linear iterative process for computing 7!.


 ```Lisp
 (define (factorial n) 
    (fact-iter 1 1 n))
(define (fact-iter product counter max-count)
(if (> counter max-count)
    product
    (fact-iter (* counter product)
            (+ counter 1)
        max-count)))
 ```

 As befro, we can use the substutution model to visualize the process of computing `6!`, as show in Figure 1.4.

 Compare the two processes. From one point of view, they seem hardly different at all. Both compute the same mathematical function on the same domain, and each requires a number of steps proportional to `n` to compute `n!`. Indeed, both processes even carry out the same sequence of multiplications, obtaining the same sequecnd of patial products. On the other hand, when we consider the "shapes" of the two processes, we find that than evolve quite differntly.

Consider the first process. The substitution model reveals a shape of expansion followed by contraction, indicated by arrow in **Figure 1.3**. The expansion occurs as the process builds up a chain of *deferred operations* (in this case, a chain of multiplications). The contraction occurs as the operations are actually performed. This type of process, charatrized by a chain of deffered operations, is called a *recursive process*. Carrion out this process requires that the interpreter keep thack of the operation to be performed later on. In the computation of `n!`, the length of the chain of deffered multiplicaion, and hence the amount of information needed to keep track of it, grows lineary with `n` (is proportional to `n`), just like the number of steps. Such a process is called a *linear recursive process*.

By contrast, the second process does not grow and shrink. At each step, all we need to keep track of, for any `n`, are thre current values of the variables `product`, `counter`, and `max-count`. We call this an `iterative process`. In general, an iterative process is one whose state can be summarized by a fixed number of `state variables`, together with a fixed rule that describes how the state variables should be updated as the process moves from state to state and an (optional) end test aht secifies conditions under which the process should termintate. In computing `n!`, the number of steps requried grows lineary with `n`. Such a proces is called `linear iterative process`.

The constrast between the two processes can be seen in anogher way. In the iterative case, the programm variables provide a complete description of the state of the process at any point. If we stopped the computation between steps, all we would need to do to resume the computation is to supply the interpreter with the value of the three program variables. Not so with the recuresime porcess. In this case there is some additional "hidden" information, maintained by the interpreter and not contained in the program variables, which indecates "where the process is" in negotiating the chain of deferred operations. The Longer the chain, the more infromation must be maintained.



